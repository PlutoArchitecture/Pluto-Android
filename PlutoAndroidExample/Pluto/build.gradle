apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply from: "dependencies.gradle"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def siteUrl = 'https://github.com/minggo620/Pluto-Android'   // 项目的主页
def gitUrl = 'https://github.com/minggo620/Pluto-Android.git'   // Git仓库的url
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())


android {
    compileSdkVersion 23
    buildToolsVersion "24.0.3"


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 7
        versionName "1.2.1"
        consumerProguardFiles 'proguard-rules.pro'

    }

}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/commons-httpclient-3.1.jar')
    compile files('libs/httpmime-4.5.2.jar')

    //这两个要在主项目里边加
    //compile thirdDependencies.butterKnife
    //apt thirdDependencies.butterknifeCompiler
    //compile thirdDependencies.autoBundle
    //apt thirdDependencies.autoBundleProcessor

    //现在的项目类似retrofit,暂时不采用
    //compile thirdDependencies.retrofit
    //compile thirdDependencies.converterGson
    //compile thirdDependencies.okhttp

    compile thirdDependencies.gson
    compile thirdDependencies.commonlang

    compile supportDependencies.design
    compile supportDependencies.recyclerView
    compile supportDependencies.cardView
    compile supportDependencies.appCompat
    compile thirdDependencies.materialprogressbar
    compile supportDependencies.supportAnnotation
    compile thirdDependencies.glide
    compile thirdDependencies.glideTransformation

}

version = "1.2.1"
group = "com.minggo"

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    pkg {
        repo = 'pluto'//自己bintray创建仓库名字
        name = 'Pluto'//Library的名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']//不能随便写，只能是仓库创建时选择的license type
        userOrg = 'minggoopen' //自己bintray创建的organization名称
        publish = true // 是否是公开项目。

        version {
            name = '1.2.1'
            desc = 'High integrated development framework for Android applications.'
            released  = new Date()
            vcsTag = 'v1.2.1'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }

    configurations = ['archives']

}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Pluto Android'
                description 'High integrated development framework for Android applications.'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'MIT'
                        url 'https://raw.githubusercontent.com/minggo620/Pluto-Android/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'minggo620'        //填写bintray或者github的用户名
                        name 'minggo'         //姓名，可以是中文
                        email 'minggo8en@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
